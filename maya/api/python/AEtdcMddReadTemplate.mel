global proc AEtdcMddReadTemplate( string $nodeName )
{
    editorTemplate -beginScrollLayout;

    editorTemplate -beginLayout "MDD File" -collapse 0;
    //editorTemplate -addControl "mddFile";
    editorTemplate -callCustom "mddFileLoad" "mddFileRefresh" "";
    editorTemplate -addControl "offset";
    editorTemplate -addControl "cycle";

    editorTemplate -endLayout;

    AEdependNodeTemplate $nodeName;

    editorTemplate -addExtraControls;
    editorTemplate -endScrollLayout;
}

global proc mddFileLoad(string $attribute) 
{
    setUITemplate -pst attributeEditorTemplate;
    
    columnLayout -rowSpacing 4;

    rowLayout -nc 3 -cw 2 210;
    
    string $desc = "Browse for a MDD cache ...";
    text -l "Mdd File"  -ann $desc lbl0;
    string $widget = `textField -ann $desc txt0`;

    string $nodenm[];
    tokenize($attribute, ".", $nodenm);

    string $node = $nodenm[0];
    string $mddFileAttr = ($node + ".mddFile");

    iconTextButton -i1 "navButtonBrowse.xpm"
        -en true 
	    -w 60 -h 20
	    -ann $desc
	    -c ("tdcBrowseMddFile(\"" + $attribute + "\");") 
	    btn0;

    connectControl -fileName $widget $mddFileAttr ;
    setParent ..;
    rowLayout -nc 1 -cal 1 "right";
    button -label "Reload" -c ("tdcMddReloadFile(\""+$attribute+ "\");");
    setParent ..;
    setUITemplate -ppt;
}

global proc tdcMddReloadFile(string $attribute)
{ 
     string $nodenm[];
    tokenize($attribute, ".", $nodenm);
    //print "reloading\n";
    string $node = $nodenm[0];
    string $origFile = `tdcMddRead -q -f $node`;
    int $origCycle = `tdcMddRead -q -c $node`;
    int $origOffset = `tdcMddRead -q -o $node`;
   
    string $deformShape[] = `listConnections -sh 1 ($node+".outputGeometry")`;

    delete $node;
    tdcMddRead  -o $origOffset -c $origCycle -f $origFile -n $node  $deformShape[0];
}

global proc mddFileRefresh(string $attribute) 
{
    string $btxt = `iconTextButton -q -c btn0`;
    string $ctxt = "tdcMddReloadFile(\"" + $attribute + "\");";
    if($btxt != $ctxt)
    {
 	 string $nodenm[];
	 tokenize($attribute, ".", $nodenm);

	 string $layoutname = ("AttrEd" + `nodeType $nodenm[0]` + "FormLayout");
	 string $cmd = "deleteUI \"" + $layoutname + "\"; " + 
                         "updateAE " + $nodenm[0] + ";";
	 evalDeferred $cmd;
    }
}

global proc tdcBrowseMddFile(string $node)
{
    string $mddFileAttr = ($node + "mddFile");

    string $singleFilter = "Mdd File (*.mdd)";
    string $result[] = `fileDialog2 -fm 1 -fileFilter $singleFilter -dialogStyle 2`;
    if (size($result) > 0) {
        setAttr $mddFileAttr -type "string" $result[0];
    }
}
